%MACRO VERIF5 (MAR)/store;

%LOCAL MAX NMAR11;

%IF &&&MAR=&MARMEN  %THEN %LET MAX=%EVAL(&NMAX1);
%IF &&&MAR=&MARIND  %THEN %LET MAX=%EVAL(&NMAX2);
%IF &&&MAR=&MARKISH %THEN %LET MAX=%EVAL(&NMAX3);

DATA __MAR1;
  SET &&&MAR %IF %UPCASE(&NONREP)=OUI %THEN %DO;(WHERE=(R NE 1)) %END; ;
  VAR=LEFT(UPCASE(VAR));
  N=INT(N)*(1-(N<0));
  TYPE="C";                               /*  VARIABLE CATÉGORIELLE  */
  IF N=0 THEN TYPE="N";                   /*  VARIABLE NUMÉRIQUE     */
  TOT=SUM(OF MAR1-MAR&MAX);

  IF TYPE="C" THEN           /*  LES VARIABLES MARN SONT-ELLES RENSEIGNÉES ?  */
  DO;
    ARRAY MAR MAR1-MAR&MAX;
    %LET ERREUR1=0;
    DO _I_=1 TO N;
       IF MAR=. THEN
       DO;
         CALL SYMPUT('ERREUR1','1');
         ERREUR="*";
       END;
    END;
  END;

  IF TYPE="N" THEN
  DO;
    %LET ERREUR3=0;
    IF MAR1=. THEN
    DO;
      CALL SYMPUT('ERREUR3','1');
      ERREUR="*";
    END;
  END;
  RUN;

  %IF &ERREUR1=1 %THEN
  %DO;
    PROC PRINT DATA=__MAR1(WHERE=(TYPE="C"));
      ID VAR;
      VAR N MAR1-MAR&MAX ERREUR;
      TITLE4 "ERREUR : POUR AU MOINS UNE VARIABLE CATÉGORIELLE, LES MARGES"
             " MAR1 À MARN,";
      TITLE5 " OÙ N EST LE NOMBRE DE MODALITÉS, NE SONT PAS"
            " TOUTES RENSEIGNÉES";
      TITLE6 " DANS LA TABLE &&&MAR SPÉCIFIÉE DANS LE PARAMETRE &MAR";
    RUN;
  %END;

  %IF &ERREUR3=1 %THEN
  %DO;
    PROC PRINT DATA=__MAR1(WHERE=(TYPE="N"));
      ID VAR;
      VAR N MAR1-MAR&MAX ERREUR;
      TITLE4 "ERREUR : POUR AU MOINS UNE VARIABLE NUMÉRIQUE, LA MARGE"
             " MAR1 N'EST PAS RENSEIGNÉE";
      TITLE5 " DANS LA TABLE &&&MAR SPÉCIFIÉE DANS LE PARAMETRE &MAR";
    RUN;
  %END;

   /*  VÉRIFICATION SUR LES TOTAUX DES MARGES DES VARIABLES CATÉGORIELLES  */

  PROC FREQ DATA=__MAR1(WHERE=(TYPE="C"));
    TABLES TOT/OUT=__MAR11 NOPRINT;
    TITLE4;
  RUN;

  %NOBSS(__MAR11,NMAR11)

  %LET ERRTOT=0;

  %IF &NMAR11>1 %THEN
  %DO;
    %LET ER=1;
    PROC PRINT LABEL DATA=__MAR1(WHERE=(TYPE="C"));
      ID VAR;
      VAR N MAR1-MAR&MAX TOT;
      LABEL TOT=TOT_MARG;
      TITLE4  "ERREUR : LES TOTAUX DES MARGES DES VARIABLES CATÉGORIELLES "
            "NE SONT PAS TOUS ÉGAUX";
      TITLE5 " DANS LA TABLE &&&MAR SPÉCIFIÉE DANS LE PARAMETRE &MAR";
    RUN;
  %END;

  %IF &NMAR11=1 %THEN
  %DO;
    %IF %UPCASE(&PCT)=OUI %THEN
    %DO;

      DATA __MAR12;
        SET __MAR11;
        ERRTOT=0;
        IF ABS(TOT-100)>0.000001 THEN ERRTOT=1;
        CALL SYMPUT ("ERRTOT",LEFT(PUT(ERRTOT,1.)));
      RUN;

      %IF &ERRTOT=1 %THEN
      %DO;

        PROC PRINT LABEL DATA=__MAR1(WHERE=(TYPE="C"));
          ID VAR;
          VAR N MAR1-MAR&MAX TOT;
          LABEL TOT=TOT_MARG;
          TITLE4  "ERREUR : LES TOTAUX DES MARGES DES VARIABLES CATÉGORIELLES "
                "NE SONT PAS ÉGAUX À 100";
          TITLE5 " DANS LA TABLE &&&MAR SPÉCIFIÉE DANS LE PARAMETRE &MAR";
        RUN;
      %END;
    %END;

  %END;
 PROC DATASETS DDNAME=WORK NOLIST;
      DELETE __MAR1 __MAR11 __MAR12 __MAR;
 QUIT;
%MEND VERIF5;
